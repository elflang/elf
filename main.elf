;; -*- mode: lisp -*-

(elf)

(lib reader compiler system)

(var eval-print (form)
  (unless (nil? form)
    (let ((ok x trace) (guard (compiler.eval form)))
      (unless ok
        (%js (print trace))
        (%lua (print (cat "error: " x "\n" trace)))
        (return))
      (def thatexpr form)
      (def that x)
      (unless (nil? x)
        (print (str x))))))

(var rep (s)
  (eval-print (reader.read-string s)))

(var repl ()
  (let buf ""
    (var rep1 (s)
      (cat! buf s)
      (let (more ()
            form (reader.read-string buf more))
          (unless (is form more)
            (eval-print form)
            (= buf "")
            (system.write "> ")))))
  (system.write "> ")
  (%js
    (let in process.stdin
      (in.setEncoding 'utf8)
      (in.on 'data rep1)))
  (%lua
    (while t
      (let s (io.read)
        (if s (rep1 (cat s "\n"))
          (break))))))

(def compile-string (chars)
  (var body (reader.read-all chars))
  (var form (compiler.expand `(do ,@body)))
  (compiler.compile form :stmt))

(def compile-file (path)
  (compile-string (system.read-file path)))

(def load (path)
  (compiler.run (compile-file path)))

(var run-file (path)
  (compiler.run (system.read-file path)))

(def elf-usage ()
  (print "usage: elf [options] <object files>")
  (print "options:")
  (print "  -c <input>\tCompile input file")
  (print "  -o <output>\tOutput file")
  (print "  -t <target>\tTarget language (default: lua)")
  (print "  -e <expr>\tExpression to evaluate")
  (system.exit))

(def elf-main ()
  (when (in? system.argv.0 '(-h --help))
    (elf-usage))
  (let (pre ()
        input nil
        output nil
        target1 nil
        expr nil
        argv system.argv
        n #argv)
    (for i n
      (let a (at argv i)
        (if (or (is a "-c") (is a "-o") (is a "-t") (is a "-e"))
            (if (is i (- n 1))
                (print (cat "missing argument for " a))
              (do (++ i)
                  (let val (at argv i)
                    (if (is a "-c") (= input val)
                        (is a "-o") (= output val)
                        (is a "-t") (= target1 val)
                        (is a "-e") (= expr val)))))
            (~is "-" (char a 0)) (add pre a))))
    (step file pre
      (run-file file))
    (if (nil? input) (if expr (rep expr) (repl))
      (do (if target1 (= target* target1))
          (let code (compile-file input)
            (if (or (nil? output) (is output "-"))
                (print code)
              (system.write-file output code)))))))

(once
  (elf-main))

