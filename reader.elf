;; -*- mode: lisp -*-

(mac defreader ((char s) rest: body)
  `(= (get read-table ,char) (fn (,s) ,@body)))

(var delimiters (set-of "[" "]" "(" ")" ";" "\n"))
(var whitespace (set-of " " "\t" "\n"))

(var stream (str more)
  (obj pos: 0 string: str len: (# str) more: more))

(var peek-char (s)
  (let ((:pos :len :string) s)
    (when (< pos len)
      (char string pos))))

(var read-char (s)
  (let c (peek-char s)
    (if c (do (inc s.pos) c))))

(var skip-non-code (s)
  (with any? nil
    (while true
      (let c (peek-char s)
        (if (nil? c) (break)
            (get whitespace c) (read-char s)
            (is c ";")
            (do (while (and c (~is c "\n"))
                  (= c (read-char s)))
                (skip-non-code s))
          (break)))
      (= any? true))))

(var read-table (obj))
(var eof (obj))

(var read (s)
  (skip-non-code s)
  (let c (peek-char s)
    (if (nil? c)
        eof
      ((or (get read-table c)
           (get read-table ""))
       s))))

(var read-all (s)
  (with l ()
    (while true
      (let form (read s)
        (if (is form eof) (break))
        (add l form)))))

(var read-string (str more)
  (let x (read (stream str more))
    (unless (is x eof) x)))

(var key? (atom)
  (and (string? atom)
       (> (# atom) 1)
       (is (char atom (edge atom)) ":")))

(var flag? (atom)
  (and (string? atom)
       (> (# atom) 1)
       (is (char atom 0) ":")))

(var expected (s c)
  (let ((:more :pos) s)
    (or more (error (cat "Expected " c " at " pos)))))

(var wrap (s x)
  (let y (read s)
    (if (is y s.more) y
      (list x y))))

(var maybe-number (str)
  (when (number-code? (code str (edge str)))
    (number str)))

(var real? (x)
  (and (number? x) (~nan? x) (~inf? x)))

(var valid-access? (str)
  (and (> (# str) 2)
       (~is "." (char str 0))
       (~is "." (char str (edge str)))
       (~search str "..")))

(var parse-index (a b)
  (let n (number a)
    (if (nil? n)
        `(get ,b (quote ,a))
      `(at ,b ,n))))

(var parse-access (str)
  (reduce parse-index (reverse (split str "."))))

(var read-atom (s)
  (let (str "" dot? false)
    (while true
      (let c (peek-char s)
        (when (or (not c) (get whitespace c) (get delimiters c))
          (break))
        (if (is c ".") (= dot? true))
        (cat! str (read-char s))))
    (if (is str "true") true
        (is str "false") false
        (is str "nan") nan
        (is str "-nan") nan
        (is str "inf") inf
        (is str "-inf") -inf
      (let n (maybe-number str)
        (if (real? n) n
            (and dot? (valid-access? str))
            (parse-access str)
          str)))))

(var read-list (s ending) 
  (read-char s)
  (with r nil
    (let l ()
      (while (nil? r)
        (skip-non-code s)
        (let c (peek-char s)
          (if (is c ending) (do (read-char s) (= r l))
              (nil? c) (= r (expected s ending))
            (let x (read s)
              (if (key? x)
                  (let (k (clip x 0 (edge x))
                        v (read s))
                    (= (get l k) v))
                  (flag? x) (= (get l (clip x 1)) true)
                (add l x)))))))))

(var read-next (s prev ws?)
  (case (peek-char s)
    "." (do (read-char s)
            (skip-non-code s)
            (if (not (peek-char s))
                (or s.more eof)
              (let x (read-atom s)
                (if (or (is x eof) (is x s.more)) x
                  (read-next s (parse-index x prev))))))
    "(" (if ws? prev
          (let x (read-list s ")")
            (if (is x s.more) x
              (read-next s `(,prev ,@x) (skip-non-code s)))))
    prev))

(defreader ("" s) ; atom
  (read-next s (read-atom s)))

(defreader ("(" s)
  (read-next s (read-list s ")") (skip-non-code s)))

(defreader (")" s)
  (error (cat "Unexpected ) at " s.pos)))

(mac %fn (body)
  (let (n -1 l () any? nil)
    (ontree skip: [unless (atom? _) (is (hd _) '%fn)]
       [when (and (string? _) (<= (# _) 2) (is (code _ 0) 95)) ; "_"
         (= any? true)
         (var c (code _ 1))
         (when (and c (>= c 48) (<= c 57)) ; "0" through "9"
           (while (< n (- c 48))
             (add l (cat "_" (chr (+ 48 (inc n)))))))
         nil]
       body)
    (when (and any? (none? l))
      (add l "_"))
  `(fn ,l ,body)))

(defreader ("[" s)
  (let x (read-list s "]")
    (if (is x s.more) x
      (read-next s `(%fn ,x) (skip-non-code s)))))

(defreader ("]" s)
  (error (cat "Unexpected ] at " s.pos)))

(defreader ("\"" s)
  (read-char s)
  (with r nil
    (let str "\""
      (while (nil? r)
        (let c (peek-char s)
          (if (is c "\"") (= r (cat str (read-char s)))
              (nil? c) (= r (expected s "\""))
            (do (when (is c "\\")
                  (cat! str (read-char s)))
                (cat! str (read-char s)))))))))

(defreader ("|" s)
  (read-char s)
  (with r nil
    (let str "|"
      (while (nil? r)
        (let c (peek-char s)
          (if (is c "|") (= r (cat str (read-char s)))
              (nil? c) (= r (expected s "|"))
            (cat! str (read-char s))))))))

(defreader ("'" s)
  (read-char s)
  (wrap s 'quote))

(defreader ("`" s)
  (read-char s)
  (wrap s 'quasiquote))

(defreader ("," s)
  (read-char s)
  (if (is (peek-char s) "@")
      (do (read-char s)
	  (wrap s 'unquote-splicing))
    (wrap s 'unquote)))

(export stream
        read
        read-all
        read-string
        read-table)
