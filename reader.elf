;; -*- mode: lisp -*-

(mac defreader ((char s) rest: body)
  `(= (get read-table ,char) (fn (,s) ,@body)))

(var delimiters (set-of "(" ")" "[" "]" "{" "}" ";" "\n"))
(var whitespace (set-of " " "\t" "\n"))

(var stream (str more)
  (obj pos: 0 string: str len: #str more: more))

(var peek-char (s)
  (let ((:pos :len :string) s)
    (when (< pos len)
      (char string pos))))

(var read-char (s)
  (let c (peek-char s)
    (if c (do (++ s.pos) c))))

(var skip-non-code (s)
  (with any? nil
    (while t
      (let c (peek-char s)
        (if (nil? c) (break)
            (get whitespace c) (read-char s)
            (is c ";")
            (do (while (and c (~is c "\n"))
                  (= c (read-char s)))
                (skip-non-code s))
          (break)))
      (= any? t))))

(var read-table (obj))
(var eof (obj))

(var read (s)
  (skip-non-code s)
  (let c (peek-char s)
    (if (nil? c)
        eof
      ((or (get read-table c)
           (get read-table ""))
       s))))

(var read-all (s)
  (if (str? s) (= s (stream s)))
  (with l ()
    (while t
      (let form (read s)
        (if (is form eof) (break))
        (add l form)))))

(var read-string (str more)
  (let x (read (stream str more))
    (unless (is x eof) x)))

(var key? (atom)
  (and (str? atom)
       (> #atom 1)
       (is (char atom (edge atom)) ":")))

(var flag? (atom)
  (and (str? atom)
       (> #atom 1)
       (is (char atom 0) ":")))

(var expected (s c)
  (let ((:more :pos) s)
    (or more (error (cat "Expected " c " at " pos)))))

(var wrap (s x)
  (let y (read s)
    (if (is y s.more) y
      (list x y))))

(var maybe-number (str)
  (when (number-code? (code str (edge str)))
    (number str)))

(var real? (x)
  (and (num? x) (~nan? x) (~inf? x)))

(var valid-access? (str)
  (and (> #str 2)
       (~is "." (char str 0))
       (~is "." (char str (edge str)))
       (~search str "..")))

(var parse-index (a b)
  (let n (number a)
    (if (nil? n)
        `(get ,b (quote ,a))
      `(at ,b ,n))))

(var parse-access (str)
  (reduce parse-index (rev (split str "."))))

(var read-atom (s)
  (let (str "" dot? false)
    (while t
      (let c (peek-char s)
        (when (or (not c) (get whitespace c) (get delimiters c))
          (break))
        (if (is c ".") (= dot? t))
        (cat! str (read-char s))))
    (if (is str "true") t
        (is str "false") false
        (is str "nan") nan
        (is str "-nan") nan
        (is str "inf") inf
        (is str "-inf") -inf
      (let n (maybe-number str)
        (if (real? n) n
            (and dot? (valid-access? str))
            (parse-access str)
          str)))))

(var read-list (s ending) 
  (read-char s)
  (with r nil
    (let l ()
      (while (nil? r)
        (skip-non-code s)
        (let c (peek-char s)
          (if (is c ending) (do (read-char s) (= r l))
              (nil? c) (= r (expected s ending))
            (let x (read s)
              (if (key? x)
                  (let (k (clip x 0 (edge x))
                        v (read s))
                    (= (get l k) v))
                  (flag? x) (= (get l (clip x 1)) t)
                (add l x)))))))))

(var read-next (s prev ws?)
  (case (peek-char s)
    "." (do (read-char s)
            (skip-non-code s)
            (if (~peek-char s)
                (or s.more eof)
              (let x (read-atom s)
                (if (or (is x eof) (is x s.more)) x
                  (read-next s (parse-index x prev))))))
    "(" (if ws? prev
          (let x (read-list s ")")
            (if (is x s.more) x
              (read-next s `(,prev ,@x) (skip-non-code s)))))
    prev))

(defreader ("" s) ; atom
  (read-next s (read-atom s)))

(defreader ("(" s)
  (read-next s (read-list s ")") (skip-non-code s)))

(defreader (")" s)
  (error (cat "Unexpected ) at " s.pos)))

(def ontree (f l :skip)
  (unless (and skip (skip l))
    (let y (f l)
      (if y (return y)))
    (unless (atom? l)
      (each x l
        (let y (ontree f x skip: skip)
          (if y (return y)))))))

(def hd-is? (l val)
  (and (list? l) (is (hd l) val)))

(mac %fn (body)
  (let (n -1 l () any? nil)
    (ontree skip: [hd-is? _ '%fn]
       [when (and (str? _) (<= (len _) 2) (is (code _ 0) 95)) ; "_"
         (= any? t)
         (var c (code _ 1))
         (when (and c (>= c 48) (<= c 57)) ; "0" through "9"
           (while (< n (- c 48))
             (add l (cat "_" (chr (+ 48 (++ n)))))))
         nil]
       body)
    (when (and any? (none? l))
      (add l "_"))
  `(fn ,l ,body)))

(defreader ("[" s)
  (let x (read-list s "]")
    (if (is x s.more) x
      (read-next s `(%fn ,x) (skip-non-code s)))))

(defreader ("]" s)
  (error (cat "Unexpected ] at " s.pos)))

(defreader ("{" s)
  (let x (read-list s "}")
    (if (is x s.more) x
      (let e x.0
        (= x (tl x))
        (while (> #x 1)
          (let ((op a rest: bs) x)
            (= e `(,op ,e ,a))
            (= x bs)))
        (read-next s e (skip-non-code s))))))

(defreader ("}" s)
  (error (cat "Unexpected } at " s.pos)))

(defreader ("\"" s)
  (read-char s)
  (with r nil
    (let str "\""
      (while (nil? r)
        (let c (peek-char s)
          (if (is c "\"") (= r (cat str (read-char s)))
              (nil? c) (= r (expected s "\""))
            (do (when (is c "\\")
                  (cat! str (read-char s)))
                (cat! str (read-char s)))))))))

(defreader ("|" s)
  (read-char s)
  (with r nil
    (let str "|"
      (while (nil? r)
        (let c (peek-char s)
          (if (is c "|") (= r (cat str (read-char s)))
              (nil? c) (= r (expected s "|"))
            (cat! str (read-char s))))))))

(defreader ("'" s)
  (read-char s)
  (wrap s 'quote))

(defreader ("`" s)
  (read-char s)
  (wrap s 'quasiquote))

(defreader ("," s)
  (read-char s)
  (if (is (peek-char s) '@)
      (do (read-char s)
	  (wrap s 'unquote-splicing))
    (wrap s 'unquote)))

(defreader ("#" s)
  (read-char s)
  (wrap s 'len))

(export stream
        read
        read-all
        read-string
        read-table)
